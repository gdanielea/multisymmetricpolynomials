{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "5d01740a",
   "metadata": {},
   "source": [
    "# A computation with multisymmetric polynomials"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "c6fc0956",
   "metadata": {},
   "source": [
    "- Authors: Daniele Agostini and Jinhyung Park\n",
    "- Version: OSCAR version 1.2.1 or newer.\n",
    "- Reference: [AP] D. Agostini, J. Park, *Determinantal ideals for secant varieties* (2025). arXiv:2510.01895"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "b105e6d4",
   "metadata": {},
   "outputs": [],
   "source": [
    "using Oscar"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "ecd8c17c-66df-4226-8b70-26e933451dbe",
   "metadata": {},
   "source": [
    "We work over the rational numbers $\\mathbb{Q}$. Let $X=\\mathbb{A}^3$ and consider the cartesian product\n",
    "$X^3 = \\mathbb{A}^9$, seen as the space of matrices\n",
    "\n",
    "$$\n",
    "\\begin{pmatrix}\n",
    "x_{11} & x_{12} & x_{13} \\\\\n",
    "x_{21} & x_{22} & x_{23} \\\\\n",
    "x_{31} & x_{32} & x_{33} \\\\\n",
    "\\end{pmatrix}\n",
    "$$\n",
    "\n",
    "where each column represents a point in $\\mathbb{A}^3$. The symmetric group on three elements $\\mathfrak{S}_3$ acts by permuting the columns. We denote by $R_3$ the coordinate ring of $X^3$."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "45b0e2e0-4649-4a2e-a86a-df49c6aafd59",
   "metadata": {},
   "outputs": [],
   "source": [
    "# We set up the coordinate ring \n",
    "\n",
    "R3, x = graded_polynomial_ring(QQ, \"x\" => ((1:3),(1:3)));\n",
    "\n",
    "# It will be also convenient to define the matrix M\n",
    "\n",
    "M3 = matrix(R3,3,3,x);"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "25b935d3-d3f9-4323-b3a6-e5f567cfe31a",
   "metadata": {},
   "source": [
    "The *partial diagonals* in $X^3$ are the sets where two of the columns of the above matrix coincide\n",
    "\n",
    "$$\n",
    "\\Delta_{ij} = \\{(p_1,p_2,p_3) \\in X^3 \\,|\\, p_i = p_j \\} \\quad \\text{ for } 1\\leq i < j \\leq 3\n",
    "$$\n",
    "\n",
    "The *big diagonal* is the union of all the partial diagonals\n",
    "\n",
    "$$\n",
    "\\Delta_3 = \\Delta_{12}\\cup \\Delta_{13} \\cup \\Delta_{23}\n",
    "$$\n",
    "\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "f9b1555c-8c81-4313-8907-b2578d6c5c16",
   "metadata": {},
   "outputs": [],
   "source": [
    "# We set up the ideals of the partial diagonals. \n",
    "\n",
    "I12 = ideal(R3,M3[:,1]-M3[:,2]);\n",
    "I13 = ideal(R3,M3[:,1]-M3[:,3]);\n",
    "I23 = ideal(R3,M3[:,2]-M3[:,3]);\n",
    "\n",
    "# We intersect them to obtain the ideal of the big diagonal\n",
    "\n",
    "ID3 = intersect(I12,I13,I23);\n",
    "\n",
    "# We can compute the minimal free resolution of the quotient R3/ID3\n",
    "\n",
    "FD3 = free_resolution(quotient_ring_as_module(ID3));\n",
    "betti(FD3)\n",
    "\n",
    "# We see that ID3 is minimally generated by 3 quadrics and 10 cubics."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "a50e10a5",
   "metadata": {},
   "source": [
    "An *alternating polynomial* is a regular function $f(p_1,p_2,p_3)$ on $X^3$ such that\n",
    "\n",
    "$$\n",
    "(\\sigma\\cdot f) = f(p_{\\sigma(1)},p_{\\sigma(2)},p_{\\sigma(3)}) = \\varepsilon(\\sigma)\\cdot f(p_1,p_2,p_3) \\quad \\text{ for all } \\sigma\\in \\Sigma_3\n",
    "$$\n",
    "\n",
    "where $\\epsilon\\colon \\mathfrak{S}_3 \\to \\{\\pm 1\\}$ is the alternating character of $\\mathfrak{S}_3$. The space of alternating polynomials is\n",
    "\n",
    "$$ R_3^{\\varepsilon_3} = \\{ f \\in R_3 \\,|\\, f \\text{ alternating } \\} $$\n",
    "\n",
    "and it generates an ideal\n",
    "\n",
    "$$\n",
    "J_3 = \\left( R_3^{\\varepsilon_3} \\right) \\subseteq I_{\\Delta_3} \\subseteq R_3\n",
    "$$\n",
    "\n",
    "where the first containment follows from the fact that any alternating polynomial vanishes on the big diagonal. We want to check whether the two ideals $J_3$ and $I_{\\Delta_3}$ coincide. \n",
    "\n",
    "To do so, we need to find generators of $J_3$: since we have already seen that $I_{\\Delta_3}$ is generated by quadrics and cubics, it is enough to consider the quadrics and cubics generators of $J_3$.\n",
    "\n",
    "There is a standard projection of graded vector spaces\n",
    "\n",
    "$$\n",
    "\\pi^{\\varepsilon_3}\\colon R_3 \\to R_3^{\\varepsilon_3}, \\quad \\pi^{\\varepsilon_3}(f) = \\sum_{\\sigma\\in \\mathfrak{S}_3} \\varepsilon(\\sigma)(\\sigma\\cdot f)\n",
    "$$\n",
    "\n",
    "hence a basis of $R_3^{\\varepsilon_3}$ is given by the image of all monomials. We write every monomial as follows: for any $j\\in \\{1,2,3\\}$ take vectors $\\mathbf{a}_j = (a_{1j},a_{2j},a_{3j})$, and define\n",
    "\n",
    "$$\n",
    " \\mathbf{x}_1^{\\mathbf{a}_1}\\mathbf{x}_2^{\\mathbf{a}_2}\\mathbf{x}_3^{\\mathbf{a}_3} := (x_{11}^{a_{11}}x_{21}^{a_{21}}x_{31}^{a_{31}})(x_{12}^{a_{12}}x_{22}^{a_{22}}x_{32}^{a_{32}})(x_{13}^{a_{13}}x_{23}^{a_{23}}x_{33}^{a_{33}})\n",
    "$$\n",
    "\n",
    "Then each $\\sigma\\in \\mathfrak{S}_3$ acts on the monomials as $\\sigma(\\mathbf{x}_1^{\\mathbf{a}_1}\\mathbf{x}_2^{\\mathbf{a}_2}\\mathbf{x}_3^{\\mathbf{a}_3}) = \\mathbf{x}_1^{\\mathbf{a}_{\\sigma(1)}}\\mathbf{x}_2^{\\mathbf{a}_{\\sigma(2)}}\\mathbf{x}_3^{\\mathbf{a}_\\sigma(3)}$. Thus\n",
    "\n",
    "$$\n",
    "\\pi^{\\varepsilon_3}(\\mathbf{x}_1^{\\mathbf{a}_1}\\mathbf{x}_2^{\\mathbf{a}_2}\\mathbf{x}_3^{\\mathbf{a}_3}) = \\sum_{\\sigma\\in \\mathfrak{S}_3} \\varepsilon(\\sigma)\\cdot (\\mathbf{x}_1^{\\mathbf{a}_{\\sigma(1)}}\\mathbf{x}_2^{\\mathbf{a}_{\\sigma(2)}}\\mathbf{x}_3^{\\mathbf{a}_\\sigma(3)}) = \n",
    "\\det\n",
    "\\begin{pmatrix}\n",
    "\\mathbf{x}_1^{\\mathbf{a}_1} & \\mathbf{x}_1^{\\mathbf{a}_2} & \\mathbf{x}_1^{\\mathbf{a}_3} \\\\\n",
    "\\mathbf{x}_2^{\\mathbf{a}_1} & \\mathbf{x}_2^{\\mathbf{a}_2} & \\mathbf{x}_2^{\\mathbf{a}_3} \\\\\n",
    "\\mathbf{x}_3^{\\mathbf{a}_1} & \\mathbf{x}_3^{\\mathbf{a}_2} & \\mathbf{x}_3^{\\mathbf{a}_3}\n",
    "\\end{pmatrix}.\n",
    "$$\n",
    "\n",
    "Since this is an alternating function of $(\\mathbf{a}_1,\\mathbf{a}_2,\\mathbf{a}_3)$, if we want a generating set of the degree $d$ part $R^{\\varepsilon_3}_d$ it is enough to take \n",
    "\n",
    "$$\n",
    "\\pi^{\\varepsilon_3}(\\mathbf{x}_1^{\\mathbf{a}_1}\\mathbf{x}_2^{\\mathbf{a}_2}\\mathbf{x}_3^{\\mathbf{a}_3}) \\qquad \\mathbf{a}_j \\text{ mutually distinct}, \\sum_j|\\mathbf{a}_j| = d, \n",
    "$$\n",
    "\n",
    "For example, let us fix the degree-lexicographic monomial ordering on the set of multi-indexes $\\mathbf{a} = (a_1,a_2,a_3)$ so that $\\mathbf{a}>\\mathbf{b}$ if and only if either $|\\mathbf{a}|>|\\mathbf{b}|$ or $|\\mathbf{a}|=|\\mathbf{b}|$ and the first nonzero entry of $\\mathbf{a}-\\mathbf{b}$ is positive. Then for a generating set of $R^{\\varepsilon_3}_d$ we can take\n",
    "\n",
    "$$\n",
    "\\pi^{\\varepsilon_3}(\\mathbf{x}_1^{\\mathbf{a}_1}\\mathbf{x}_2^{\\mathbf{a}_2}\\mathbf{x}_3^{\\mathbf{a}_3}), \\quad \\mathbf{a}_1 > \\mathbf{a}_2 > \\mathbf{a}_3, \\,\\, \\sum_j |\\mathbf{a}_j|=d\n",
    "$$\n",
    "\n",
    "If $d=0,1$ we get zero generators. If instead $d=2$ we have $3$ possible choices:\n",
    "\n",
    "$$\n",
    "\\begin{matrix}\n",
    "\\mathbf{a}_1 & \\mathbf{a}_2 & \\mathbf{a}_3 \\\\\n",
    "(1,0,0) & (0,1,0) & (0,0,0)\\\\\n",
    "(1,0,0) & (0,0,1) & (0,0,0)\\\\\n",
    "(0,1,0) & (0,0,1) & (0,0,0)\\\\\n",
    "\\end{matrix}\n",
    "$$\n",
    "\n",
    "If $d=3$ we have 19 possible choices:\n",
    "\n",
    "$$\n",
    "\\begin{matrix}\n",
    "\\mathbf{a}_1 & \\mathbf{a}_2 & \\mathbf{a}_3 \\\\\n",
    "(1,0,0)& (0,1,0)& (0,0,1)\\\\\n",
    "(2,0,0)& (1,0,0)& (0,0,0)\\\\\n",
    "(2,0,0)& (0,1,0)& (0,0,0)\\\\\n",
    "(2,0,0)& (0,0,1)& (0,0,0)\\\\\n",
    "(1,1,0)& (1,0,0)& (0,0,0)\\\\\n",
    "(1,1,0)& (0,1,0)& (0,0,0)\\\\\n",
    "(1,1,0)& (0,0,1)& (0,0,0)\\\\\n",
    "(1,0,1)& (1,0,0)& (0,0,0)\\\\\n",
    "(1,0,1)& (0,1,0)& (0,0,0)\\\\\n",
    "(1,0,1)& (0,0,1)& (0,0,0)\\\\\n",
    "(0,2,0)& (1,0,0)& (0,0,0)\\\\\n",
    "(0,2,0)& (0,1,0)& (0,0,0)\\\\\n",
    "(0,2,0)& (0,0,1)& (0,0,0)\\\\\n",
    "(0,1,1)& (1,0,0)& (0,0,0)\\\\\n",
    "(0,1,1)& (0,1,0)& (0,0,0)\\\\\n",
    "(0,1,1)& (0,0,1)& (0,0,0)\\\\\n",
    "(0,0,2)& (1,0,0)& (0,0,0)\\\\\n",
    "(0,0,2)& (0,1,0)& (0,0,0)\\\\\n",
    "(0,0,2)& (0,0,1)& (0,0,0)\\\\\n",
    "\\end{matrix}\n",
    "$$\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "ac5e4dce-9bf6-4f21-9e53-36debc4509ca",
   "metadata": {},
   "outputs": [],
   "source": [
    "# The following function takes a 3x3 matrix A of nonnegative integers and the corresponding column vectors a_1,a_2,a_3 and\n",
    "# and it outputs the corresponding alternating polynomial det(x_i^{a_j})\n",
    "\n",
    "function alternating(A)\n",
    "    Z = zero_matrix(R3,3,3);\n",
    "    for i in 1:3\n",
    "        for j in 1:3\n",
    "            Z[i,j] = 1\n",
    "            for k in 1:3;\n",
    "                Z[i,j] = Z[i,j]*x[k,i]^(A[j,k]);\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "    return det(Z);\n",
    "end\n",
    "\n",
    "# Now we write down the list of all possible choices in degree two and three\n",
    "\n",
    "Choices2 = (\n",
    "[[1,0,0] , [0,1,0] , [0,0,0]],\n",
    "[[1,0,0] , [0,0,1] , [0,0,0]],\n",
    "[[0,1,0] , [0,0,1] , [0,0,0]],\n",
    ");\n",
    "\n",
    "Choices3 = (\n",
    "[[1,0,0], [0,1,0], [0,0,1]],\n",
    "[[2,0,0], [1,0,0], [0,0,0]],\n",
    "[[2,0,0], [0,1,0], [0,0,0]],\n",
    "[[2,0,0], [0,0,1], [0,0,0]],\n",
    "[[1,1,0], [1,0,0], [0,0,0]],\n",
    "[[1,1,0], [0,1,0], [0,0,0]],\n",
    "[[1,1,0], [0,0,1], [0,0,0]],\n",
    "[[1,0,1], [1,0,0], [0,0,0]],\n",
    "[[1,0,1], [0,1,0], [0,0,0]],\n",
    "[[1,0,1], [0,0,1], [0,0,0]],\n",
    "[[0,2,0], [1,0,0], [0,0,0]],\n",
    "[[0,2,0], [0,1,0], [0,0,0]],\n",
    "[[0,2,0], [0,0,1], [0,0,0]],\n",
    "[[0,1,1], [1,0,0], [0,0,0]],\n",
    "[[0,1,1], [0,1,0], [0,0,0]],\n",
    "[[0,1,1], [0,0,1], [0,0,0]],\n",
    "[[0,0,2], [1,0,0], [0,0,0]],\n",
    "[[0,0,2], [0,1,0], [0,0,0]],\n",
    "[[0,0,2], [0,0,1], [0,0,0]],\n",
    ");\n",
    "\n",
    "# Finally we take the ideal generated by all these 3+19=22 alternating polynomials\n",
    "\n",
    "J3p = ideal(R3,0)\n",
    "for A in Choices2\n",
    "    J3p = J3p + ideal(alternating(matrix(A)));\n",
    "end\n",
    "for A in Choices3\n",
    "    J3p = J3p + ideal(alternating(matrix(A)));\n",
    "end\n",
    "\n",
    "# This ideal is generated by alternating polynomials, so it is contained in the ideal J3.\n",
    "\n",
    "is_subset(J3p,ID3)\n",
    "\n",
    "# If this ideal contains the ideal ID3, then it must be that J3p = J3 = ID3\n",
    "\n",
    "is_subset(ID3,J3p)\n",
    "\n",
    "# The output is true, and we are done."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "f1d507c7",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3 (ipykernel)",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.10.12"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
